10 DIM BO(1,3)
20 ON SPRITE GOSUB 5000
40 DATA 1F,3F,7F,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,7F,3F,1F,F8,FC,FE,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF
50 DATA FE,FC,F8,07,0C,0C,07,03,0F,1F,37,67,C7,07,07,06,06,0E,1E,E0,30,30,E0,C0,F0,F8,EC,E6,E3
60 DATA E0,E0,60,60,70,78
70 INTERVAL ON
80 SPRITE ON
90 GOSUB 1000 : ' INITIALIZE GAME '
100 NB=0
110 GOSUB 2000 : ' ADD ENEMY LIGHT '
120 XC=126
130 YC=96
140 FOR I= 2 TO 8 
150   XD(I)=SGN(5-I)
160   YD(8-I)=XD(I)
170 NEXT
180 SWAP YD(0),YD(8)
185 ' GAME LOOP 
190 FOR I=0 TO 1
200   TB=NB
210   I=0
220   GOSUB 3000 : ' MOVE LIGHTS
230   S=STICK(J)
240   XC=XC+XD(S)
250   YC=YC+YD(S)
260   GOSUB 4000 : ' MOVE HERO
270 NEXT
1000 ' INITIALIZE GAME '
1010 SCREEN 3:CLS
1020 OPEN"GRP:" FOR OUTPUT AS #1
1030 PSET(68,76)
1040 PRINT#1,"RUN!"
1050 FOR I=1 TO 1000:NEXT
1060 CLOSE #1
1070 SCREEN 2,3,0:COLOR 15,1,6:CLS
1080 S=BASE(9)
1090 RESTORE 40
1100 FOR M=0 TO 63
1110   READ R$
1120   VPOKE M+S,VAL("&H"+R$)
1130 NEXT
1140 RETURN 
2000 ' ADD ENEMY LIGHT '
2010 PLAY "V15O6CDCD"
2020 IF NB=2 THEN RETURN 
2030 IF NB=0 THEN BO(NB,0)=33:BO(NB,1)=16:BO(NB,2)=4:BO(NB,3)=-4:NB=NB+1:RETURN 
2040 BO(NB,0)=220:BO(NB,1)=16:BO(NB,2)=-4:BO(NB,3)=-4:NB=NB+1:RETURN
3000 ' MOVE LIGHTS '
3010 TB=TB-1
3020 IF BO(TB,0)>220 OR BO(TB,0)<6 THEN BO(TB,MA2)=BO(TB,2)*-1 
3030 IF BO(TB,1)>152 OR BO(TB,1)<5 THEN BO(TB,3)=BO(TB,3)*-1 
3040 BO(TB,0)=BO(TB,0)+BO(TB,2):BO(TB,1)=BO(TB,1)+BO(TB,3)
3050 PUTSPRITE TB+1,(BO(TB,0),BO(TB,1)),10,0
3060 IF TB>0 THEN 3010 ELSE RETURN  
4000 ' MOVE HERO 
4010 IF XC <-32 OR XC>256 OR YC <-32 OR YC > 192 THEN 6000 ELSE PUT SPRITE 0,(XC,YC),4,1:RETURN
5000 ' COLISION DETECTION '
5010 SPRITE OFF
5020 INTERVAL OFF 
5040 IF(ABS(XC-BO(0,0))<31 AND ABS(YC-BO(0,1))<31) OR (ABS(XC-BO(1,0))<31 AND ABS(YC-BO(1,1))<31) THEN 7000 
5050 IF BO(0,2)<0 AND BO(1,2)<0 THEN BO(0,2)=BO(0,2)*-1
5060 IF BO(0,3)<0 AND BO(1,3)<0 THEN BO(0,3)=BO(0,3)*-1
5070 IF BO(0,2)>0 AND BO(1,2)>0 THEN BO(1,2)=BO(1,2)*-1
5080 IF BO(0,3)>0 AND BO(1,3)>0 THEN BO(1,3)=BO(1,3)*-1
5090 FOR N=0 TO 1
5100   BO(N,0)=BO(N,0)+BO(N,2)
5110   BO(N,1)=BO(N,1)+BO(N,3)
5120 NEXT
5130 SPRITE ON
5140 INTERVAL ON
5150 RETURN
6000 ' PRISIONER ESCAPED '
6010 SPRITE OFF
6020 INTERVAL OFF
6030 BEEP
6040 SCREEN 3
6050 CLS
6060 OPEN "GRP:" FOR OUTPUT AS#1
6070 PSET(75,40)
6080 PRINT#1,"YOU"
6090 PSET(15,90)
6100 PRINT#1,"ESCAPED!"
6110 CLOSE#1
6120 FOR I=1 TO 5000:NEXT
6130 GOTO 20
7000 ' PRISIONER CAPTURED '
7010 BEEP
7020 SCREEN 3
7030 CLS
7040 OPEN "GRP:" FOR OUTPUT AS#1
7050 PSET(68,76)
7060 PRINT#1,"BANG!"
7070 CLOSE#1
7080 FOR I=1 TO 200:NEXT
7090 SPRITE OFF
7100 INTERVAL OFF
7110 GOTO 20