0010 SCREEN 2
0020 BLOAD"LABEL.SC2",S
0030 OP$=INPUT$(1)
0040 SCREEN 0:COLOR 15,1,1:KEY OFF:CLS
0050 CLEAR
0060 ?"-------------------------------"
0070 ?"ARVORE FRACTAL                 "
0080 ?"MANOEL NETO 2023-12-30         "
0090 ?"-------------------------------"
0100 A=3.14159/2:'90o EM RADIANOS
0110 G=0:'GERACAO ATUAL
0120 DX=0:DY=0:'DELTA PROXIMO PONTO
0130 NX=0:NY=0:'PROXIMO PONTO
0140 X=127:Y=191:'PONTO INICIAL
0150 INPUT "VARIACAO ANGULAR  (0.7):";DA
0160 INPUT "TAMANHO DO TRONCO (70) :";L
0170 INPUT "GERACAO MAXIMA    (6)  :";GM
0180 INPUT "REDUCAO DO TRONCO (0.6):";SK
0190 PRINT "GERAR COM OS SEGUINTES PARAMETROS?"
0200 PRINT "VARIACAO ANGULAR  :";DA
0210 PRINT "TAMANHO DO TRONCO :";L
0220 PRINT "GERACAO MAXIMA    :";GM
0230 PRINT "REDUCAO DO TRONCO :";SK
0240 PRINT "(S/N)>";:OP$=INPUT$(1)
0250 IF OP$="S" OR OP$="s" THEN GOTO 270 
0260 GOTO 10
0270 DIM XS(GM):'MANTEM AS COORDENADAS
0280 DIM YS(GM):'A CADA GERACAO
0290 SCREEN 2 
0300 GOSUB 1000:'GERACOES RECURSIVAS 
0310 OP$=INPUT$(1)
0320 GOTO 10

1000 ' GERACOES RECURSIVAS 
1010 DX=COS(A)*L: 'CALCULA DELTA X
1020 DY=SIN(A)*L: 'CALCULA DELTA Y
1030 NX=X+DX:NY=Y-DY
1040 LINE (X,Y)-(NX,NY)
1050 XS(G)=X:YS(G)=Y
1060 X=NX:Y=NY
1070 G=G+1
1080 A=A+DA
1090 L=L*SK
1100 IF G<=GM THEN GOSUB 1000
1110 ' RAMIFICA PARA O OUTRO LADO
1120 A=A-DA*2
1130 IF G<=GM THEN GOSUB 1000
1140 ' VOLTA UMA GERACAO 
1150 A=A+DA
1160 L=L/SK
1170 G=G-1
1180 X=XS(G):Y=YS(G)
1190 RETURN
