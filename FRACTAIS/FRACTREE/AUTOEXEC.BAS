0005 SCREEN 0:COLOR 15,1,1:KEY OFF:CLS
0007 CLEAR
0010 ?"-------------------------------"
0020 ?"ARVORE FRACTAL                 "
0030 ?"MANOEL NETO 2023-12-30         "
0040 ?"-------------------------------"
0050 A=3.14159/2:'90o EM RADIANOS
0060 G=0:'GERACAO ATUAL
0070 DX=0:DY=0:'DELTA PROXIMO PONTO
0080 NX=0:NY=0:'PROXIMO PONTO
0090 X=127:Y=191:'PONTO INICIAL
0100 INPUT "VARIACAO ANGULAR  (0.7):";DA
0110 INPUT "TAMANHO DO TRONCO (70) :";L
0120 INPUT "GERACAO MAXIMA    (6)  :";GM
0130 INPUT "REDUCAO DO TRONCO (0.6):";SK
0140 PRINT "GERAR COM OS SEGUINTES PARAMETROS?"
0150 PRINT "VARIACAO ANGULAR  :";DA
0160 PRINT "TAMANHO DO TRONCO :";L
0170 PRINT "GERACAO MAXIMA    :";GM
0180 PRINT "REDUCAO DO TRONCO :";SK
0190 PRINT "(S/N)>";:OP$=INPUT$(1)
0200 IF OP$="S" OR OP$="s" THEN GOTO 220 
0210 GOTO 5
0220 DIM XS(GM):'MANTEM AS COORDENADAS
0230 DIM YS(GM):'A CADA GERACAO
0240 SCREEN 2 
0250 GOSUB 1000:'GERACOES RECURSIVAS 
0260 OP$=INPUT$(1)
0270 GOTO 5

1000 ' GERACOES RECURSIVAS 
1010 DX=COS(A)*L: 'CALCULA DELTA X
1020 DY=SIN(A)*L: 'CALCULA DELTA Y
1030 NX=X+DX:NY=Y-DY
1040 LINE (X,Y)-(NX,NY)
1050 XS(G)=X:YS(G)=Y
1060 X=NX:Y=NY
1070 G=G+1
1080 A=A+DA
1090 L=L*SK
1100 IF G<=GM THEN GOSUB 1000
1110 ' RAMIFICA PARA O OUTRO LADO
1120 A=A-DA*2
1130 IF G<=GM THEN GOSUB 1000
1140 ' VOLTA UMA GERACAO 
1150 A=A+DA
1160 L=L/SK
1170 G=G-1
1180 X=XS(G):Y=YS(G)
1190 RETURN
