0010 ' -------------------------------
0020 ' ARVORE FRACTAL 
0030 ' MANOEL NETO 2023-12-30
0040 ' -------------------------------
0050 SCREEN 2:COLOR 15,1,1
0060 A=3.14159/2:'90O EM RADIANOS
0070 DA=0.7:'VARIACAO ANGULAR
0080 L=80:'TAMANHO DO TRONCO 
0090 G=0:GM=7:'GERACOES ATUAL/MAXIMA
0100 DX=0:DY=0:'DELTA PROXIMO PONTO
0110 NX=0:NY=0:'PROXIMO PONTO
0120 SK=0.60:'REDUCAO DO TRONCO
0130 X=127:Y=191:'PONTO INICIAL
0140 DIM XS(GM):'MANTEM AS COORDENADAS
0150 DIM YS(GM):'A CADA GERACAO 
0160 GOSUB 1000:'GERACOES RECURSIVAS 
0999 GOTO 999 

1000 ' GERACOES RECURSIVAS 
1010 DX=COS(A)*L: 'CALCULA DELTA X
1020 DY=SIN(A)*L: 'CALCULA DELTA Y
1030 NX=X+DX:NY=Y-DY
1040 LINE (X,Y)-(NX,NY)
1050 XS(G)=X:YS(G)=Y
1060 X=NX:Y=NY
1070 G=G+1
1080 A=A+DA
1090 L=L*SK
1100 IF G<=GM THEN GOSUB 1000
1110 ' RAMIFICA PARA O OUTRO LADO
1120 A=A-DA*2
1130 IF G<=GM THEN GOSUB 1000
1140 ' VOLTA UMA GERACAO 
1150 A=A+DA
1160 L=L/SK
1170 G=G-1
1180 X=XS(G):Y=YS(G)
1190 RETURN