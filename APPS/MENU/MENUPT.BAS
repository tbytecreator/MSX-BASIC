5 ' MarMSX 2020
10 SCREEN 1:COLOR 15,0,0:KEYOFF
15 ' Copia forma dos caracteres - A -> a
20 FOR C=&H41*8 TO &H5A*8
30 VPOKE C+256,VPEEK(C)
40 NEXT C
45 ' Modifica fundo minusculas + bloco
49 CL=&H1F : ' Texto preto / Barra branca
50 VPOKE 8204,CL:VPOKE 8205,CL:VPOKE 8206,CL:VPOKE 8207,CL
60 VPOKE 8219,CL/16+(CL-(CL AND &HF0))*16
75 ' Carrega opcoes 
80 OP=0:MX=0
90 READ D$: IF D$="**" THEN 140
100 A$(MX,1)=LEFT$(D$+STRING$(7,CHR$(32)),8)
110 FOR I=1 TO LEN(D$):MID$(D$,I,1)=CHR$(ASC(MID$(D$,I,1))+32):NEXT I
120 A$(MX,2)=LEFT$(D$+STRING$(7,CHR$(219)),8)
130 MX=MX+1:GOTO 90
135 ' Cria o menu
140 LOCATE 2,1:PRINT"FORMA"
150 FOR F=0 TO MX
160 LOCATE 2,F+3:PRINT A$(F,1)
170 NEXT F
175 ' Loop principal
180 LOCATE 2,OP+3:PRINT A$(OP,2)
190 K$=INKEY$: IF K$="" THEN 190
200 K=ASC(k$): IF K=30 OR K=31 THEN LOCATE 2,OP+3:PRINT A$(OP,1)
210 IF K=31 THEN OP=(OP+1) MOD MX
220 IF K=30 THEN OP=OP-1:IF OP=-1 THEN OP=MX-1
230 IF K=13 THEN 500
240 GOTO 180
300 DATA LINHA,CIRCULO,RAIO,PONTO,ARCO,**
500 ' Opcao escolhida
510 SCREEN 0
520 PRINT"A opcao escolhida foi: ";A$(OP,1)
530 END
