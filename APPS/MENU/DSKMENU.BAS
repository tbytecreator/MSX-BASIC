
120 CLEAR 2048
130 ST=0 :' DRIVE: 0=A, 1=B, 2=C, 3=D
140 A1=PEEK(62290)*256+PEEK(62289):'ENDEREÇO DMA 
150 DIM F$(111):'VETOR DE ARQUIVOS'
160 L=0 'files counter
170 FOR T=0 TO 111 : ' LIMPA O ARRAY COM ESPAÇOS
180   F$(T)=SPACE$(12)
190 NEXT
200 A$=DSKI$(ST,1):'O DRIVE EH 360 OU 720?
210 D=PEEK(A1)
220 IF D=248 THEN Z=5 ELSE Z=7
230 FR=DSKF(0):'ESPACO LIVRE
240 FOR SK=0 TO 6
250   AD=A1
260   A$=DSKI$(ST,SK+Z):'LE O SETOR
270   FOR P=0 TO 15
280     IF PEEK(AD)<32 OR PEEK(AD)>126 THEN AD=AD+32:GOTO 360
290     P$=""
300     FOR W=0 TO 10:'LE O NOME
310       P$=P$+CHR$(PEEK(AD))
320       AD=AD+1
330     NEXT
340     F$(L)=LEFT$(P$,8)+"."+RIGHT$(P$,3)
350     L=L+1:AD=AD+21
360   NEXT
370 NEXT
380 FOR T=0 TO L-1:'IMPRIME OS ARQUIVOS
390   PRINT F$(T)
400 NEXT
410 PRINT
420 PRINT"File(s):";L
430 PRINT"Free:";FR;"kB"
440 OP$=INPUT$(1)
450 RUN "AUTOEXEC.BAS"