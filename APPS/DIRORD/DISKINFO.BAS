005 ' --------------------------------
010 ' INFORMACOES DE DISCO
015 ' --------------------------------
020 MAXFILES=2
030 OPEN "NUL" AS 1
040 FIELD1,3ASJP$,8ASN$,2ASBS$,1ASSC$,2ASRS$,1ASFS$,2ASRD$,2ASTS$,1ASMD$,2ASSF$,2ASST$,2ASDS$
070 CLOSE
080 M=VARPTR(JP$)
090 POKE &HF351,PEEK(M+1)
100 POKE &HF352,PEEK(M+2)
110 CLS
120 ? STRING$(39,"-")
130 ? "INFORMACOES DO DISCO  "
140 ? STRING$(39,"-")
150 ? "DESCRICAO DA MIDIA   :";HEX$(ASC(MD$))
170 ? "NOME DO OEM          :";N$
180 ? "CABECAS              :";CVI(DS$)
190 ? "SETORES/TRILHAS      :";CVI(ST$)
200 ? "BYTES/SETORES        :";CVI(BS$)
210 ? "SETORES/CLUSTERS     :";ASC(SC$)
220 ? "SETORES RESERVADOS   :";CVI(RS$)
230 ? "TOTAL DE SETORES     :";CVI(TS$)
240 ? "NUMERO DE FATs       :";ASC(FS$)
250 ? "SETORES/FAT          :";CVI(SF$)
260 ? "ENTRADAS DIR RAIZ    :";CVI(RD$)
270 ? STRING$(39,"-")
290 OP$=INPUT$(1)
300 RUN"AUTOEXEC.BAS"