10 REM *** Missel Ataque ***
30 KEYOFF:DEFINTA-Z:COLOR14,1,1
40 OPEN"grp:"FOROUTPUTAS#1
50 HI=1000
60 DIMQ(50),W(50),DR(50),K(50)
70 GOSUB1810:REM Instrucoes
80 SC=0:N=6:HT=0:AH=0:YM=2:PM=3
90 GOSUB120:REM Draw Screen
100 GOSUB300
110 GOSUB460:REM Game
120 SCREEN2,0
130 FORI=0TO255
140 H=INT(RND(1)*10)+10
150 LINE(I,159)-(I,159-H),4
160 NEXTI
170 RESTORE220
180 B$=""
190 FORI=1TO8:READA$:A=VAL("&b"+A$):B$=B$+CHR$(A):NEXTI
200 SPRITE$(0)=B$
210 RETURN
220 DATA 00001000
230 DATA 00001000
240 DATA 00001000
250 DATA 01110111
260 DATA 00001000
270 DATA 00001000
280 DATA 00001000
290 DATA 00000000
300 P=RND(-TIME)
310 LINE(120,139)-(137,149),1,BF
320 DRAW"c3bm123,149e5f5l10"
330 REM paint(128,146),3
340 BL=N+5
350 FORI=1TOBL:R=(I*4)+16:PRESET(R,162):PRINT#1,CHR$(217):NEXT
360 FORI=1TON:K(I)=1:Q(I)=INT(RND(1)*235)+12:W(I)=0:PSET(Q(I),W(I)),11:G=INT(RND(1)*2):IFG=0THENDR(I)=1:NEXTELSEDR(1)=-1:NEXT
370 X=128:Y=80
380 PUTSPRITE0,(X,Y),14,0
390 COLOR12
400 DRAW"bm12,182":PRINT#1,"Score=";SC
410 COLOR11
420 DRAW"bm120,182":PRINT#1,"High=";HI
430 RN=0
440 RETURN
450 REM Start Game
460 ONSTRIGGOSUB1070,1070
470 STRIG(C)ON
480 ONINTERVAL=5GOSUB570
490 INTERVALON
500 REM Main Loop
510 RG=INT(RND(1)*50)
520 IFRG<2ANDRE=0THENGOSUB780
530 IFRE=1THENGOSUB810
540 IFRE=0THENGOSUB930
550 GOTO510
560 REM Move Cross
570 S=STICK(C)
580 IFS=0THENRETURN
590 IFS=1THENY=Y-PM:GOSUB690:GOTO670
600 IFS=2THENX=X+PM:Y=Y-PM:GOSUB690:GOSUB710:GOTO670
610 IFS=3THENX=X+PM:GOSUB710:GOTO670
620 IFS=4THENX=X+PM:Y=Y+PM:GOSUB710:GOSUB730:GOTO670
630 IFS=5THENY=Y+PM:GOSUB730:GOTO670
640 IFS=6THENX=X-PM:Y=Y+PM:GOSUB750:GOSUB730:GOTO670
650 IFS=7THENX=X-PM:GOSUB750:GOTO670
660 IFS=8THENX=X-PM:Y=Y-PM:GOSUB750:GOSUB690
670 PUTSPRITE0,(X,Y),14,0
680 RETURN
690 IFY<0THENY=0
700 RETURN
710 IFX>246THENX=246
720 RETURN
730 IFY>120THENY=120
740 RETURN
750 IFX<10THENX=10
760 RETURN
770 REM Rogue Set Up
780 RN=RN+1:IFRN>4THENRETURN
790 RE=1:RM=PM+2:RX=INT(RND(1)*200)+27:RY=0:PSET(RX,RY),12:RETURN
800 REM Move Rogue
810 BEEP:RY=RY+RM:IFRY>138THENGOSUB860
820 RD=INT(RND(1)*2):IFRD=0ANDRX>12THENRX=RX-1ELSEIFRX<250THENRX=RX+1
830 IFRE=1THENPSET(RX,RY),12
840 RETURN
850 REM Rogue Hit City
860 GOSUB1410:FORI=1TO10:CIRCLE(RX,RY),I,INT(RND(1)*14)+2:NEXTI
870 RE=0:HT=HT+1
880 FORI=1TO10:CIRCLE(RX,RY),I,1:NEXT
890 BEEP
900 IFAG=1ANDAH=NTHENGOTO1460
910 RETURN
920 REM Move Missel
930 A=INT(RND(1)*N)+1
940 IFK(A)=0THENGOTO930
950 IFX<13ORX>253THENGOTO980
960 Z=INT(RND(1)*10)
970 IFZ<1THENDR(A)=DR(A)*-1
980 V=Q(A):Z=W(A)
990 W(A)=W(A)+YM
1000 IFW(A)>138THENGOTO1300:REM Hit City
1010 IFK(A)=0THENRETURN
1020 Q(A)=Q(A)+DR(A)
1030 IFQ(A)<12ORQ(A)>254THENDR(A)=DR(A)*-1
1040 LINE(V,Z)-(Q(A),W(A)),11
1050 RETURN
1060 REM Fire
1070 IFBL=0THENPLAY"m50v15t255o7s8f":RETURN
1080 G=X:H=Y
1090 PLAY"s14m1000f"
1100 LINE(128,143)-(G+4,H+3),13
1110 FORI=1TO7:CIRCLE(G+4,H+3),I,9:NEXTI
1120 IFRE=1ANDPOINT(RX,RY)=9THENRE=0
1130 FORI=1TON:IFK(I)=0THENGOTO1150
1140 IFPOINT(Q(I),W(I))=9THENGOSUB1230
1150 NEXTI
1160 FORI=1TO7:CIRCLE(G+4,H+3),I,1:NEXT
1170 LINE(128,143)-(G+4,H+3),1
1180 BL=BL-1
1190 J=((BL+1)*4)+16:LINE(J,162)-(J+6,170),1,BF
1200 IFAH=NANDRE=0THENFORI=1TO50:NEXT:GOTO1460
1210 RETURN
1220 REM Hit Missel
1230 K(I)=0:SC=SC+N:AH=AH+1:IFAH=NTHENAG=1
1240 LINE(60,182)-(119,190),1,BF
1250 INTERVALOFF
1260 DRAW"bm60,182":PRINT#1,SC
1270 INTERVALON
1280 RETURN
1290 REM Hit City
1300 K(A)=0
1310 GOSUB1410
1320 FORI=1TO10:CIRCLE(Q(A),W(A)),I,6:NEXTI
1330 HT=HT+1
1340 AH=AH+1
1350 FORI=1TO10:CIRCLE(Q(A),W(A)),I,1:NEXTI
1360 FORI=1TO500:NEXT
1370 BEEP
1380 IFAH=NTHENGOTO1460
1390 RETURN
1400 REM Explosion Sound
1410 SOUND0,0:SOUND1,5:SOUND2,0:SOUND3,13:SOUND4,255:SOUND5,15:SOUND6,30:SOUND7,0:SOUND8,16:SOUND9,16:SOUND10,16:SOUND1,0:SOUND12,5:SOUND13,0
1420 FORI=0TO30:NEXT
1430 SOUND12,56:SOUND13,0
1440 RETURN
1450 REM Finished Level
1460 STRIG(C)OFF:INTERVALOFF
1470 PUTSPRITE0,(X,Y),0,0
1480 LINE(0,0)-(255,139),1,BF
1490 LINE(60,182)-(119,190),1,BF
1500 COLOR3
1510 DRAW"bm60,30":PRINT#1,"Hit on City=";HT
1520 IFHT>4THENGOTO1650
1530 COLOR9
1540 DRAW"bm80,50":PRINT#1,"Bonus=";(N+10)+(BL*20)
1550 SC=SC+(N*10)+(BL*20)
1560 COLOR13
1570 DRAW"bm80,65":PRINT#1,"Score=";SC
1580 PLAY"l6t255s0m10000eecccecccffedr6r6ffeedr6r6ggffeeddcr6r6r6","l6t255v12o5cegecegeceo4bo5do4ao5cecceo4bo5do4ao5ceco4ao5co4bo5dcegec"
1590 FORI=1TO8000:NEXT
1600 N=N+1:AH=0:AG=0
1610 IFN/3=INT(N/3)THENYM=YM+1:PM=PM+1
1620 LINE(0,0)-(255,139),1,BF
1630 GOTO100
1640 REM Dead
1650 GOSUB1410:FORI=1TO30:CIRCLE(128,145),I,INT(RND(1)*14)+2:NEXTI
1660 FORI=1TO30:CIRCLE(128,145),I,1:NEXT
1670 BEEP
1680 COLOR6
1690 DRAW"bm75,50":PRINT#1,"Game Over"
1700 DRAW"bm80,65":PRINT#1,"Score=";SC
1710 IFSC>HITHENCOLOR7:DRAW"bm60,75":PRINT#1,"A new high score!":HI=SC
1720 LINE(160,183)-(250,190),1,BF
1730 DRAW"bm160,182":PRINT#1,HI
1740 PLAY"t220v15o5c2c4o4a#8a8o5c4f4e4c8c4c4c4o4a8a#8o5c4f4e4c4c4f4a8g8fac4d4d8cd4o4g4g4a4a#4o5c4o4a4f.."
1750 COLOR10
1760 DRAW"bm20,100":PRINT#1,"Press SPACE or FIRE to start"
1770 IFSTRIG(0)THENC=0:GOTO80
1780 IFSTRIG(1)THENC=1:GOTO80
1790 GOTO1770
1800 REM Instrucoes
1810 SCREEN0,,0:WIDTH40
1820 FORI=22TO0STEP-1
1830 LOCATE10,I:PRINTCHR$(175);"Missile Attack";CHR$(174)
1840 LOCATE10,I+1:PRINT"                  "
1850 NEXT I
1860 LOCATE1,3
1870 PRINT"Your city is being attacked  by";
1880 PRINT" aliens,who are dropping missles on";
1890 PRINT" it. You mustuse your laser to dest";
1900 PRINT"roy the missiles."
1910 PRINT:PRINT" Use the cursor keys or  joystick to";
1920 PRINT" po-sition the  sights  and then  press ";
1930 PRINT" thespace bar or fire button to";
1940 PRINT" shoot."
1950 PRINT:PRINT" You have a limited amount of lasers";
1960 PRINT" andit only takes five hits to ";
1970 PRINT" destroy  thecity."
1980 PRINT:PRINT" Beware of rogue missels. These are";
1990 PRINT" veryfast. A siren warns of falling";
2000 PRINT"rogues."
2010 LOCATE3,21:PRINT"Press SPACE or FIRE to play"
2020 IFSTRIG(0)THENC=0:GOTO2050
2030 IFSTRIG(1)THENC=1:GOTO2050
2040 GOTO2020
2050 PLAY"t250s9m10000o4c3f8g8a4a4g4a#4a4c4o3a#4o4a8f8g3o3a#4a4o3e4f."
2060 RETURN