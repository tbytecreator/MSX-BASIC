00010 ' ====================================
00020 ' ALIEMINATION
00030 ' MANOEL NETO 2025
00040 ' ====================================
00050 COLOR 15,1,1
00060 KEY OFF:CLS:DEFINT A-Z
00070 BLOAD"XBASIC.BIN",R
00080 DEFUSR1=&H41 : ' FUNCAO DESLIGA TELA
00090 DEFUSR2=&H44 : ' FUNCAO LIGA TELA

00100 ' ====================================
00110 ' SPLASH SCREEN
00120 ' ====================================
00130 SCREEN 1,2
00140 LOCATE 6,8:? "SPACE INVADERS"
00150 LOCATE 4,9:? "PRESS FIRE TO START"
00160 LOCATE 6,10:? "MANOEL NETO 2024"
00170 OP$=INPUT$(1)
00180 CLS

00200 ' ====================================
00210 ' INICIALIZA JOGO
00220 ' ====================================
00230 DIM EX(5,11),EY(5,11)
00240 AL=1: ' ANIMACAO DOS ALIENS
00250 CA=2: ' COLUNA DOS ALIENS 
00260 LA=1: ' LINHA DOS ALIENS 
00270 DR=1: ' DIRECAO DOS ALIENS 
00280 CC=0: ' CONTADOR DE COLUNAS
00290 HX=127: HY=174: HV=16 : ' X E Y DO HEROI
00300 GOSUB 6000 : ' CARREGA TABELA DE PADROES
00310 GOSUB 7000 : ' CARREGA TABELA DE CORES
00320 GOSUB 8000 : ' CARREGA TABELA DE SPRITES
00330 ' ======= POSICIONA OS ALIENS =======
00340 FOR I = 0 TO 4
00350    FOR J = 0 TO 10
00360        EX(I,J)=(J+1)*CA
00370        EY(I,J)=LA+I
00380    NEXT J
00390 NEXT I
00400 CALL TURBO ON (EX(),EY(),HX,HY,HV)

00500 ' ====================================
00510 ' GAME LOOP  
00520 ' ====================================
00540 ' == VERIFICA A DIRECAO DOS ALIENS ===
00550 IF CC=7 THEN DR=-1:LA=LA+1
00560 IF CC=0 THEN DR=1:LA=LA+1
00570 ' ====== VERIICA O FIM DO JOGO =======
00580 IF LA >= 20 THEN 5000

00600 ' ============ METODO DRAW ===========
00610 A=USR1(0) : ' DESLIGA A TELA
00620 ' ===== DESENHA OS SOFT SPRITES ======
00630 FOR I = 0 TO 4
00640    FOR J = 0 TO 10
00650       CL=EX(I,J):LN=EY(I,J)
00660       VPOKE &H1800+CL+LN*32,0
00670       CL=CL+DR
00680       LN=LA+I
00690       VPOKE &H1800+CL+LN*32,AL
00700       EX(I,J)=CL
00710       EY(I,J)=LN
00720    NEXT J
00730 NEXT I
00740 ' ===== DESENHA OS HARD SPRITES ======
00750 PUT SPRITE 0,(HX,HY),6,0 : 'CANHAO
00760 A=USR2(1) : ' LIGA A TELA
00770 '========== FIM METODO DRAW ==========

00800 '========= METODO UPDATE  ============
00810 S=STICK(0) : ' ENTRADA DO JOGADOR 
00820 IF S = 3 THEN GOTO 850 : ' MOVE DIREITA
00830 IF S = 7 THEN GOTO 860 : ' MOVE ESQUERDA
00840 GOTO 870
00850 HX=HX+HV:GOTO 870
00860 HX=HX-HV
00870 IF HX<0 THEN HX=0
00880 IF HX>239 THEN HX=239
00890 IF AL=2 THEN AL=1 ELSE AL=2 
00900 CC=CC+DR : ' CONTADOR DE COLUNAS
00910 TIME = 0 
00920 IF TIME<60 THEN 920
00930 GOTO 500
00940 '====== FIM METODO UPDATE  ===========

05000 CALL TURBO OFF
05010 ' ====================================
05020 ' FIM DO JOGO
05030 ' ====================================
05040 CLS
05050 LOCATE 6,8:? "GAME OVER"
05060 LOCATE 4,9:? "PRESS FIRE TO PLAY AGAIN"
05070 OP$=INPUT$(1)
05080 GOTO 100

06000 ' ====================================
06010 ' CARREGA A TABELA DE PADROES 
06020 ' ====================================
06030 RESTORE 9000
06040 S=0 
06050 READ R$
06060 IF R$="*" THEN RETURN ELSE VPOKE S,VAL("&H"+R$):S=S+1
06070 GOTO 6050

07000 ' ====================================
07010 ' CARREGA A TABELA DE CORES  
07020 ' ====================================
07030 FOR I=0 TO 31
07040   VPOKE &H2000+I,32
07050 NEXT I
07060 RETURN 

08000 ' ====================================
08010 ' CARREGA TABELA DE PADROES DE SPRITES
08020 ' ====================================
08030 RESTORE 10000
08040 S=BASE(9)
08050 READ R$
08060 IF R$="*" THEN RETURN ELSE VPOKE S,VAL("&H"+R$):S=S+1
08070 GOTO 8050

09000 ' ====================================
09010 ' SOFT SPRITES           
09020 ' ====================================
09030 ' ALIENS - COLOR 7
09040 DATA 00,00,00,00,00,00,00,00
09050 DATA 18,3C,7E,DB,FF,24,5A,A5
09060 DATA 18,3C,7E,DB,FF,5A,81,42
09070 DATA *

10000 ' ====================================
10010 ' HARD SPRITES
10020 ' ====================================
10030 ' CANHAO - COLOR 6
10040 DATA 00,00,00,00,00,00,00,00
10050 DATA 01,01,07,07,1F,1F,7F,7F
10060 DATA 00,00,00,00,00,00,00,00
10070 DATA 80,80,E0,E0,F8,F8,FE,FE
10080 DATA *