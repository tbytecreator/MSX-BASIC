0010 ' ===============================
0020 ' GAME LOOP 
0030 ' MANOEL NETO 2024
0040 ' ===============================
0050 COLOR 15,1,1
0060 KEY OFF:CLS:DEFINT A-Z

0100 ' ******************************
0110 ' TITLE SCREEN
0120 ' ******************************
0130 SCREEN 0
0140 LOCATE 14,10:? "SPACE INVADERS"
0150 LOCATE 10,12:? "PRESS FIRE TO START"
0160 LOCATE 12,14:? "MANOEL NETO 2024"
0170 OP$=INPUT$(1)

0200 ' ******************************
0210 ' INICIALIZA JOGO
0220 ' ******************************
0230 SCREEN 2,2,0
0240 GOSUB 8000 : ' CARREGA SPRITES
0250 ON SPRITE GOSUB 7000 :'COLISOES
0260 X=127:Y=174:V=16    :'HEROI
0270 TIME=0:AN=2:CM=1:VX=20:VY=20
0280 DIM EX(4,3),EY(4,3)
0290 FOR I = 0 TO 4
0300    FOR J = 0 TO 3
0310        EX(I,J)=16+J*20
0320        EY(I,J)=16+I*20
0330   NEXT J
0340 NEXT I

0400 ' ******************************'
0410 ' ENTRADA DO JOGADOR            '
0420 ' ******************************'
0430 S=STICK(0) 
0440 IF S = 3 THEN GOTO 470
0450 IF S = 7 THEN GOTO 480
0460 GOTO 520
0470 X=X+V:GOTO 520
0480 X=X-V:GOTO 520
0490 ' ******************************'
0500 ' DETERMINA OS LIMITES DA TELA  '
0510 ' ******************************'
0520 IF X<0 THEN X=0
0530 IF X>239 THEN X=239

0600 ' ******************************'
0610 ' ATUALIZA AS COORDENADAS       '
0620 ' ******************************'
0630 FOR I = 0 TO 4
0640    FOR J = 0 TO 3
0650        EX(I,J)=EX(I,J)+VX
0660    NEXT J
0670 NEXT I
0680 IF EX(0,3)>=223 THEN 800 
0690 IF EX(0,0)<=16  THEN 800
0700 GOTO 900 

0800 ' ******************************
0810 ' AJUSTA OS ALIENS
0820 ' ******************************
0830 VX=VX*-1
0840 FOR I = 0 TO 4
0850    FOR J = 0 TO 3
0860        EY(I,J)=EY(I,J)+VY
0870    NEXT J
0880 NEXT I

0900 ' ******************************'
0910 ' DESENHA O FRAME               '
0920 ' ******************************'
0930 SPRITE OFF:CM=1
0940   PUT SPRITE 0,(X,Y),6,0
0950   FOR I = 0 TO 4
0960     FOR J = 0 TO 3
0970       PUT SPRITE CM,(EX(I,J),EY(I,J)),3,AN
0980       CM=CM+1
0990     NEXT J
1000   NEXT I
1010 SPRITE ON
1020 IF TIME<30 GOTO 1020
1030 IF AN=1 THEN AN=2 ELSE AN=1
1040 TIME=0 
1050 GOTO 400 

7000 ' ******************************'
7010 ' TRATA AS COLISOES             '
7020 ' ******************************'
7030 SPRITE OFF
7040 COLOR 15,1,1
7050 SCREEN 0
7060 CLS
7070 ?"COLIDIU"
7080 END

8000 ' ******************************'
8010 ' CARREGA DIRETO NO VDP         '
8020 ' ******************************'
8030 S=BASE(9)
8040 READ R$
8050 IF R$="*" THEN RETURN ELSE VPOKE S,VAL("&H"+R$):S=S+1
8060 GOTO 8040

9000 ' ******************************'
9010 ' PADRAO DOS SPRITES            '
9020 ' ******************************'
9030 ' CANHAO - COLOR 6
9040 DATA 00,00,00,00,00,00,01,01
9050 DATA 07,07,07,7F,FF,FF,FF,FF
9060 DATA 00,00,00,00,00,00,80,80
9070 DATA E0,E0,E0,FE,FF,FF,FF,FF
9130 ' ALIEN 1-1 - COLOR 3
9140 DATA 00,01,07,1F,13,3F,7F,7E
9150 DATA 1C,18,20,30,18,0C,06,02
9160 DATA 00,80,E0,F8,C8,FC,FE,7E
9170 DATA 38,18,04,0C,18,30,60,40
9180 ' ALIEN 1-2 - COLOR 3
9190 DATA 00,01,07,1F,13,3F,7F,7F
9200 DATA 1F,03,06,0C,18,30,60,40
9210 DATA 00,80,E0,F8,C8,FC,FE,FE
9220 DATA F8,C0,60,30,18,0C,06,02
9999 DATA *