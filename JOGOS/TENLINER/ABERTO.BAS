10 DIMBO(2,3):DATA1F,3F,7F,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,7F,3F,1F,F8,FC,FE,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,FE,FC,F8,07,0C,0C,07,03,0F,1F,37,67,C7,07,07,06,06,0E,1E,E0,30,30,E0,C0,F0,F8,EC,E6,E3,E0,E0,60,60,70,78

100 GOSUB 1000                      : ' INITIALIZE GAME 
110 GOSUB 2000                      : ' ADD ENEMY
120 IF T MOD 20 = 0 THEN GOSUB 2000 : ' PUT NEW ENEMY EVERY 20 ITERATIONS
130 GOSUB 3000                      : ' UPDATE OBJECTS 
140 GOSUB 4000                      : ' DRAW SPRITES 
150 T=T+1:IF T=30 THEN T=0          : ' CONTROL INTERATION
160 GOTO 120                        : ' GAME LOOP  

1000 ' INITIALIZE GAME 
1010 SCREEN 2,2,0:COLOR 15,1,6:CLS
1020 S=BASE(9)
1030 RESTORE 10
1040 FOR M=0 TO 63
1050   READ R$
1060   VPOKE M+S,VAL("&H"+R$)
1070 NEXT
1080 MS$="RUN!":GOSUB 6000
1090 NB=0:XC=126:YC=96:T=0:FORI=0TO1:BO(I,0)=0:BO(I,1)=0:BO(I,2)=0:BO(I,3)=0
1100 FORI=2TO8:XD(I)=SGN(5-I):YD(8-I)=XD(I):NEXT:SWAPYD(0),YD(8)
1110 ON SPRITE GOSUB 5000
1120 SPRITE ON
1130 RETURN

2000 ' PUT NEW ENEMY 
2010 IF NB=3 THEN RETURN
2020 IF NB=1 THEN BO(NB,0)=220:BO(NB,1)=16:BO(NB,2)=-5:BO(NB,3)=-5:NB=NB+1:RETURN
2030 BO(NB,0)=33:BO(NB,1)=16:BO(NB,2)=5:BO(NB,3)=-5:NB=NB+1:RETURN

3000 ' UPDATE OBJECTS
3010 S=STICK(J)
3020 XC=XC+XD(S)
3030 YC=YC+YD(S)
3040 IF XC < -32 OR XC>256 OR YC < -32 OR YC > 192 THEN MS$="YOU ESCAPED!":GOSUB 6000:GOTO 100 
3050 TB=NB
3060 TB=TB-1
3070 IF BO(TB,0)>220 OR BO(TB,0)<4 THEN BO(TB,2)=BO(TB,2)*-1 
3080 IF BO(TB,1)>168 OR BO(TB,1)<4 THEN BO(TB,3)=BO(TB,3)*-1 
3090 BO(TB,0)=BO(TB,0)+BO(TB,2)
3100 BO(TB,1)=BO(TB,1)+BO(TB,3)
3110 IF TB>0 GOTO 3060
3120 RETURN

4000 ' DRAW SPRITES
4020 PUT SPRITE 0,(XC,YC),4,1
4030 FOR S=1 TO NB:PUT SPRITE S,(BO(S-1,0),BO(S-1,1)),10,0:NEXT
4050 RETURN 

5000 ' COLISION DETECTION
5010 SPRITE OFF 
5020 FOR I=0 TO NB-1
5030   IF BO(I,0)+8 > XC AND XC+16>=BO(I,0) AND BO(I,1)+8 > YC AND YC+16 >= BO(I,1) THEN MS$="BANG!":GOSUB 6000:GOTO 100
5040   BO(I,2)=BO(I,2)*-1:BO(I,3)=BO(I,3)*-1:BO(I,0)=BO(I,0)+BO(I,2):BO(I,1)=BO(I,1)+BO(I,3):NEXT
5050 SPRITE ON 
5060 RETURN

6000 ' PRINT MESSAGE
6010 BEEP:CLS:OPEN"GRP:"FOROUTPUTAS#1:PSET(10,2):PRINT#1,MS$:CLOSE#1:FORI=1TO1000:NEXT:RETURN