1000 SCREEN 0:CLS
1001 KEY 1,"D &H":KEY 2,"M &H":KEY 3,"L &H":KEY 5,"E"+CHR$(13):KEY 4,"?"+CHR$(13)
1004 ' FAZ O HELP E PEDE UM COMANDO NO PROMPT
1005 GOSUB 3000: ' DA O PRIMEIRO HELP 
1010 ?:LINE INPUT">";A$:IF LEN(A$)=0 THEN 1010
1020 C$=MID$(A$,1,1)
1025 IF C$="L" THEN 1130 ELSE IF C$="D" THEN 1030 ELSE IF C$="M" THEN 1060 ELSE IF C$="?" THEN 1004 ELSE IF C$="E" THEN GOTO 4000 ELSE BEEP:?"?";:GOTO 1010
1030 ' COMANDO DUMP
1031 E=VAL(MID$(A$,2))
1040 FOR J=1 TO 8
1041   ? RIGHT$("0000"+HEX$(E),4);" ";
1042   FOR I=0 TO 3
1043     ? RIGHT$("00"+HEX$(PEEK(I+E)),2);" ";
1044   NEXT I
1045   FOR I=0 TO 3
1046     IF PEEK(I+E)>32 THEN ? CHR$(PEEK(I+E));" "; ELSE ?"  ";
1047   NEXT I
1048   ?
1049   E=E+4
1050 NEXT J
1051 I$ =INPUT$(1):IF I$=CHR$(27) THEN 1010 ELSE ?:GOTO 1040
1060 ' COMANDO MODIFY
1061 E=VAL(MID$(A$,2))
1070 ? RIGHT$("0000"+HEX$(E),4);" ";RIGHT$("00"+HEX$(PEEK(E)),2);" ";
1080 H$=""
1081 FOR I = 1 TO 2
1090   I$=INPUT$(1):IF I=1 THEN IF I$=CHR$(13) THEN E=E+1 GOTO 1070 ELSE IF I$=CHR$(8) THEN E=E-1:GOTO 1070
1100   IF I$=CHR$(27) THEN ?:GOTO 1010
1110   IF I$>="0" AND I$<="9" OR I$>="A" AND I$<="F" THEN 1120 ELSE 1090
1120   H$=H$+I$:?I$;
1121 NEXT I
1122 ?
1123 POKE E,VAL("&H"+H$):E=E+1:GOTO 1070
1130 ' COMANDO LOAD
1131 E=VAL(MID$(A$,2))
1140 RESTORE
1141 FOR I=0 TO 7:READ A$(I),B$(I):NEXT I
1142 FOR I=0 TO 3:READ C$(I),E$(I):NEXT I
1143 FOR I=0 TO 7:READ D$(I),F$(I):NEXT I
1144 READ G$(0),G$(1),G$(2),H$(0),H$(1),H$(2),I$(0),I$(1),J$(0),J$(1)
1150 FOR K=1 TO 20
1151   C=PEEK(E)
1160   ? USING "#####";E;:?" ";:GOSUB 1190:IF F>0 THEN I=I+1
1170   ? TAB(22);
1171   FOR X=0 TO I-1
1172     I$=HEX$(PEEK(X+E))
1173     IF LEN(I$)<2 THEN I$="0"+I$
1180     ? I$;" ";
1181   NEXT X
1182   ?:E=E+I
1183 NEXT K
1184 ?
1185 I$=INPUT$(1):IF I$=CHR$(27) THEN 1010 ELSE 1150 
1190 REM SEPARA OS GRUPOS 
1200 F=0:IF C=221 THEN F=1 ELSE IF C=253 THEN F=2
1210 C$(2)="HL":IF F=1 THEN C$(2)="IX" ELSE IF F=2 THEN C$(2)="IY"
1220 IF C=203 THEN 1740 ELSE IF C=237 THEN 1800
1230 IF F>0 AND PEEK(E+1)=203 THEN 1740 
1240 IF F>0 THEN C=PEEK(E+1)
1250 REM COMANDOS DIRETOS 
1260 E$(2)=C$(2)
1270 I=1
1271 IF C=39 THEN ? "DAA"; ELSE IF C=47 THEN ? "CPL"; ELSE IF C=249 THEN ? "LD SP,";C$(2); ELSE IF C=227 THEN ? "EX (SP),";C$(2); ELSE IF C=118 THEN ? "HALT"; ELSE IF C=201 THEN ? "RET";ELSE IF C=31 THEN ? "RRA";ELSE 1290
1280 RETURN 
1290 IF C=235 THEN ? "EX DE,HL";ELSE IF C=8 THEN ? "EX AF,AF";ELSE IF C=217 THEN ? "EXX";ELSE IF C=233 THEN ? "JP (";C$(2);")";ELSE IF C=0 THEN ? "NOP";ELSE IF C=243 THEN ? "DI";ELSE IF C=251 THEN ? "EI";ELSE 1310
1300 RETURN
1310 IF C=7 THEN ? "RLCA";ELSE IF C=15 THEN ? "RRCA";ELSE IF C=23 THEN ? "RLA";ELSE IF C=55 THEN ? "SCF";ELSE IF C=63 THEN ? "CCF";ELSE 1330
1320 RETURN 
1330 I=2
1331 IF C=211 THEN ? "OUT(";:GOSUB 1610:?"),A" ELSE IF C=219 THEN ? "IN A,(";:GOSUB 1610:?")";ELSE 1350
1340 RETURN 
1350 IF C=195 THEN ? "JP ";ELSE IF C=205 THEN ?"CALL ";ELSE 1380
1360 ?MID$(STR$(PEEK (E+1)+256*PEEK (E+2)),2,5);:I=3:RETURN
1370 RETURN
1380 IF C=16 THEN ?"DJNZ";ELSE IF C=24 THEN ?"JR ";ELSE 1410
1390 X=PEEK(E+1):IF X<128 THEN Y=E+X+2 ELSE Y=E+X-254
1400 ? MID$(STR$(Y),2,5);:I=2:RETURN 
1410 IF C>127 AND C<192 THEN X=C\8-16:Y=C MOD 8:PRINT A$(X);:IF X=0 OR X=1 OR X=3 THEN ?" A,";ELSE PRINT" "; ELSE 1430 
1420 PRINT B$(Y);:I=1:RETURN 
1430 IF C\64>0 THEN 1570
1440 IF C=34 THEN E=E+SGN(F):?"LD (";:GOSUB 1360:?"),";C$(2);:ELSE IF C=42 THEN E=E+SGN(F):?"LD ";C$(2);",(";:GOSUB 1360:?")";:ELSE 1460
1450 E=E-SGN(F):RETURN
1460 IF C=50 THEN ?"LD (";:GOSUB 1360:?"),A";ELSE IF C=58 THEN PRINT"LD A,(";:GOSUB 1360:?")";:RETURN ELSE 1480
1470 I=3:RETURN 
1480 X=C MOD 8:IF X=4 THEN PRINT "INC";ELSE IF X=5 THEN PRINT "DEC";ELSE 1500
1490 Y=C\8:PRINT " ";B$(Y);:I=1:RETURN 
1500 X=C MOD 16:IF X=3 THEN ?"INC ";ELSE IF X=11 THEN PRINT "DEC ";ELSE IF X=9 THEN ?"ADD ";C$(2);",";ELSE 1520
1510 X=C\16:?C$(X);:I=1:RETURN  
1520 IF C MOD 8=6 AND F=0 THEN ?"LD ";B$(C\8);",";:GOTO 1610 
1530 IF C MOD 16=1 THEN E=E+SGN(F):?"LD ";C$(C\16);",";:GOSUB 1360 ELSE 1550
1540 E=E-SGN(F):RETURN 
1550 IF C MOD 16=2 THEN PRINT "LD (";C$(C\16);"),A";ELSE IF C MOD 16=10 THEN ?"LD A,(";C$(C\16);")";ELSE 1570
1560 I=1:RETURN 
1570 IF C\32=1 AND C MOD 8=0 THEN ?"JR ";D$(C\8-4);",";:GOTO 1390
1580 IF C\64<>3 THEN 1680
1590 IF C MOD 8<>6 THEN 1620 
1600 I=2:X=C\8-24:?A$(X);" ";:IF X<2 OR X=3 THEN ?"A,";
1610 ?MID$(STR$(PEEK(E+1)),2,3);:RETURN
1620 X=C MOD 8:IF X=1 THEN ?"POP ";E$(C\16-12);ELSE IF X=5 THEN ?"PUSH ";E$(C\16-12);
1630 IF X=1 OR X=5 THEN I=1:RETURN 
1640 IF X=7 THEN ?"RST ";HEX$(C-199);"H";:I=1:RETURN
1650 IF X=0 THEN ?"RET ";D$(C\8-24);:I=1:RETURN
1660 IF X=2 THEN ?"JP ";ELSE ?"CALL ";
1670 ? D$(C\8-24);",";:GOTO 1360
1680 IF F=0 AND C\64=1 THEN X=(C-64)\8:?"LD ";B$(X);:X=(C-64) MOD 8:?",";B$(X);:I=1:RETURN
1690 I=2:C=PEEK(E+1):IF F>0 AND C MOD 8=6 AND C>63 THEN ?"LD ";B$(C\8-8);",(";C$(2);"+";:E=E+1:GOSUB 1610:?")"; ELSE 1710
1700 E=E-1:RETURN
1710 IF C\8=14 THEN E=E+1:?"LD (";C$(2);"+";:GOSUB 1610:PRINT "),"; B$(C-112);ELSE 1730
1720 GOTO 1700 
1730 I=3: IF C=54 THEN ?"LD (";C$(2);"+";:E=E+1:GOSUB 1610:?"),";:E=E+1:GOSUB 1610:E=E-1:GOTO 1720 ELSE 1960
1740 ' COMANDOS APOS CBH
1750 Z=1:IF F>0 THEN Z=3
1760 I=2:C=PEEK(E+Z):IF C<64 AND F=0 THEN ? F$(C\8);" ";B$(C MOD 8);:RETURN
1770 I=3:IF C<64 AND F>0 AND (C-6) MOD 8 = 0 THEN ? F$(C\8);" (";C$(2);"+";:E=E+1:GOSUB 1610:?")";:E=E-1:RETURN
1780 C=PEEK(E+Z):IF F=0 THEN ? G$(C\64-1);" ";CHR$(48+(C\8) MOD 8);",";B$(C MOD8);:I=2:RETURN
1790 IF (C-6) MOD 8=0 THEN ? G$(C\64-1);" ";CHR$(48+(C\8) MOD 8);",(";C$(2);"+";:E=E+1:GOSUB 1610: ?")";:E=E-1:RETURN ELSE 1960
1800 ' COMANDOS APOS EDH
1810 I=2:C=PEEK(E+1):IF C<64 OR C=221 OR C=253 THEN 1960 ELSE IF C>187 THEN 1920 
1820 IF C=70 THEN PRINT "IM 0";ELSE IF C=86 THEN ? "IM 1"; ELSE IF C=94 THEN ?"IM 2";ELSE IF C=77 THEN ?"RETI";ELSE IF C=69 THEN ? "RETN"; ELSE IF C=103 THEN ?"RRD"; ELSE IF C=111 THEN ?"RLD"; ELSE 1840 
1830 RETURN
1840 IF C=71 THEN ?"LD I,A";ELSE IF C=79 THEN ?"LD R,A"; ELSE IF C=87 THEN ?"LD A,I";ELSE IF C=95 THEN ?"LD A,R";ELSE 1860 
1850 RETURN 
1860 H$(3)="OUT":IF C>175 THEN H$(3)="OUT"
1870 IF C>159 AND C MOD 8<4 THEN ?H$(C MOD4);I$((C MOD 16)\8);J$((C-160)\16);:RETURN 
1880 IF C\64>1 THEN 1960 ELSE IF C MOD 8>1 THEN 1920 
1890 IF C=112 OR C=113 THEN 1960
1900 IF C MOD 8=0 THEN ?"IN ";B$(C\8-8);",(C)";ELSE ?"OUT (C)";B$(C\8-8);
1910 RETURN
1920 X= C MOD 16:Y=C\16-4:IF X=10 THEN ?"ADC HL,";C$(Y);ELSE IF X=2 THEN ?"SBC HL,";C$(Y);ELSE 1940
1930 RETURN 
1940 IF X=3 THEN ?"LD (";:E=E+1:GOSUB 1360:E=E-1:?"),";C$(C\16-4);ELSE IF X=11 THEN ?"LD ";C$(C\16-4);",(";:E=E+1:GOSUB 1360:E=E-1:?")"; ELSE 1960
1950 I=4:RETURN 
1960 ?"Z80 ?";:RETURN 
1970 ' DADOS DA MATRIZ ALFA 
1980 DATA ADD,B,ADC,C,SUB,D,SBC,E,AND,H,XOR,L,OR,(HL),CP,A
1990 DATA BC,BC,DE,DE,HL,HL,SP,AF
2000 DATA NZ,RLC,Z,RRC,NC,RL,C,RR,PO,SL,A,PE,SRA,P,SLI,M,SRL
2010 DATA BIT,RES,SET 
2020 DATA LD,CP,IN,I,D, ,R 
3000 ' ROTINA DE HELP 
3001 ?"====================================="
3002 ?"TByteCreator ASM Monitor"
3003 ?"Manoel Neto 2022-12-19"
3004 ?"====================================="
3005 ?"Comandos:                            "
3006 ?"(D)ump   =>endereco em hexa &HXXXX   "
3007 ?"(M)odify =>endereco em hexa &HXXXX   "
3008 ?"(L)oad   =>endereco em hexa &HXXXX   "
3009 ?"(E)nd                                "
3010 ?"(?) Este HELP!                       "
3011 ?"====================================="
3012 RETURN
4000 ' FINALIZA O PROGRAMA
4010 KEY 1,"color":KEY 2,"auto":KEY 3,"goto":KEY 5,"list"+CHR$(13):KEY 4,"run"+CHR$(13)
4020 CLS
4030 END
